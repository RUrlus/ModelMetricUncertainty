CMAKE_MINIMUM_REQUIRED(VERSION 3.16)

IF (NOT DEFINED MMU_CORE_VERSION_INFO)
    SET(MMU_CORE_VERSION_INFO "0.1.0")
ENDIF ()

PROJECT(
    mmu_core
    VERSION ${MMU_CORE_VERSION_INFO}
    DESCRIPTION "Fast binary classification metrics."
    LANGUAGES CXX
)

SET(CMAKE_MODULE_PATH_SAVED ${CMAKE_MODULE_PATH})
LIST(INSERT CMAKE_MODULE_PATH 0 "${PROJECT_SOURCE_DIR}/../cmake")
################################################################################
#                                   SETTINGS                                   #
################################################################################
SET(CMAKE_CXX_STANDARD 14)
SET(CMAKE_CXX_FLAGS  "${CMAKE_CXX_FLAGS} -fPIC")

include(CheckCXXCompilerFlag)
SET(MMU_RELEASE_FLAGS "")
CHECK_CXX_COMPILER_FLAG("-march=native" COMPILER_SUPPORTS_MARCH_NATIVE)
IF(COMPILER_SUPPORTS_MARCH_NATIVE)
    SET(MMU_RELEASE_FLAGS "${MMU_RELEASE_FLAGS} -march=native")
ENDIF()
CHECK_CXX_COMPILER_FLAG("-mtune=native" COMPILER_SUPPORTS_MTUNE_NATIVE)
IF(COMPILER_SUPPORTS_MTUNE_NATIVE)
    SET(MMU_RELEASE_FLAGS "${MMU_RELEASE_FLAGS} -mtune=native")
ENDIF()
CHECK_CXX_COMPILER_FLAG("-ftree-vectorize" COMPILER_SUPPORTS_FTREE)
IF(COMPILER_SUPPORTS_FTREE)
    SET(MMU_RELEASE_FLAGS "${MMU_RELEASE_FLAGS} -ftree-vectorize")
ENDIF()
CHECK_CXX_COMPILER_FLAG("-mavx2" COMPILER_SUPPORTS_MAVX2)
IF(COMPILER_SUPPORTS_MAVX2)
    SET(MMU_RELEASE_FLAGS "${MMU_RELEASE_FLAGS} -mavx2")
ENDIF()

FIND_PACKAGE(Python3 COMPONENTS Development NumPy REQUIRED)
# Needed to ensure the same Python executable is found by Pybind11
IF (NOT DEFINED PYTHON_EXECUTABLE)
    SET(PYTHON_EXECUTABLE ${Python3_EXECUTABLE})
ENDIF ()

FIND_PACKAGE(pybind11 CONFIG REQUIRED)
FIND_PACKAGE(OpenMP)

SET(MODNAME "_mmu_core")
ADD_DEFINITIONS(-DEXTENSION_MODULE_NAME=${MODNAME})
SET(PROJ_INCLUDE_DIR ${PROJECT_SOURCE_DIR}/mmu)
SET(EXT_INCLUDE_DIR ${PROJECT_SOURCE_DIR}/extern)

pybind11_add_module(${MODNAME}
    MODULE
        ${PROJ_INCLUDE_DIR}/metrics/metrics.hpp
        ${PROJ_INCLUDE_DIR}/metrics/confusion_matrix.hpp
        ${PROJECT_SOURCE_DIR}/mmu/bindings.cpp
)

TARGET_LINK_LIBRARIES(${MODNAME} PUBLIC pybind11::pybind11 OpenMP::OpenMP_CXX Python3::NumPy Python3::Python)
TARGET_INCLUDE_DIRECTORIES(${MODNAME} PUBLIC ${PROJ_INCLUDE_DIR})
TARGET_COMPILE_DEFINITIONS(${MODNAME} PRIVATE VERSION_INFO=${MMU_VERSION_INFO})
TARGET_COMPILE_OPTIONS(${MODNAME} PRIVATE "$<$<CONFIG:RELEASE>:${MMU_RELEASE_OPTIONS}>")

# Set required C++14 flag
SET_PROPERTY(TARGET ${MODNAME} PROPERTY CXX_STANDARD 14)
SET_PROPERTY(TARGET ${MODNAME} PROPERTY CXX_STANDARD_REQUIRED ON)
SET_PROPERTY(TARGET ${MODNAME} PROPERTY CXX_EXTENSIONS OFF)

IF(CALL_FROM_SETUP_PY)
  # cmake-build-extension sets the full absolute path as CMAKE_INSTALL_PREFIX.
  SET(MMU_CORE_INSTDIR "${CMAKE_INSTALL_PREFIX}")
ELSE()
  SET(MMU_CORE_INSTDIR "${PROJECT_SOURCE_DIR}/mmu/lib")
endif()

MESSAGE(STATUS "Installing _mmu_core in ${MMU_CORE_INSTDIR}")

# Setup installation path
INSTALL(TARGETS ${MODNAME} DESTINATION "${MMU_CORE_INSTDIR}")
